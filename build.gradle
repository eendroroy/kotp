plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.20"
    id "com.adarshr.test-logger" version "3.0.0"
    id "org.jetbrains.dokka" version "1.4.32"

    id 'java'
    id "maven-publish"
    id "signing"
}

repositories {
    mavenCentral()
}

group = "com.github.eendroroy"
version = "0.1.3"
sourceCompatibility = "1.8"

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("commons-codec:commons-codec:1.15")
    testImplementation("org.junit.jupiter:junit-jupiter:5.7.2")
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

testlogger {
    slowThreshold = 10
}

test {
    useJUnitPlatform()
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("$buildDir/docs"))
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from dokkaHtml
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    sign configurations.archives
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

publishing {
    def ossUser = project.findProperty("oss.user") ?: System.getenv("OSS_USERNAME")
    def ossKey = project.findProperty("oss.key") ?: System.getenv("OSS_TOKEN")

    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = ossUser
                password = ossKey
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = "OTP Generator"
                url = "https://github.com/eendroroy/kotp.git"

                licenses {
                    license {
                        name = "MIT"
                        url = "http://opensource.org/licenses/MIT"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/eendroroy/kotp.git"
                    developerConnection = "scm:git:https://github.com/eendroroy/kotp.git"
                    url = "https://github.com/eendroroy/kotp.git"
                }

                developers {
                    developer {
                        id = "eendroroy"
                        name = "indrajit"
                        email = "eendroroy@gmail.com"
                    }
                }
            }
        }
    }
}
