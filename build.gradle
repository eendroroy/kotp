plugins {
    id "maven"
    id "maven-publish"
    id "signing"
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "com.adarshr.test-logger" version "2.1.0"
    id "com.jfrog.bintray" version "1.8.5"
    id "org.jetbrains.dokka" version "0.10.1"
}

repositories {
    mavenCentral()
    jcenter()
}

group "com.github.eendroroy"
version "0.1.2"
sourceCompatibility = "1.8"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "commons-codec:commons-codec:1.14"
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
}

task javadocJar(type: Jar) {
    archiveClassifier = "javadoc"
    from dokka
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/eendroroy/kotp")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.java)
            artifact(sourcesJar)
        }
    }
}

bintray {
    user = project.findProperty("bintray.user") ?: System.getenv("USERNAME")
    key = project.findProperty("bintray.key") ?: System.getenv("TOKEN")
    publications = ["maven"]
    pkg {
        repo = project.group
        name = project.name
        userOrg = user
        licenses = ["MIT"]
        vcsUrl = "https://github.com/eendroroy/kotp.git"
        labels = ["otp-generator"]
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = "OTP generator"
        }
    }
}

uploadArchives {
    def ossUser = project.findProperty("oss.user") ?: System.getenv("OSS_USERNAME")
    def ossKey = project.findProperty("oss.key") ?: System.getenv("OSS_TOKEN")
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossUser, password: ossKey)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossUser, password: ossKey)
            }

            pom.project {
                name project.name
                packaging "jar"
                description "OTP Generator"
                url "https://github.com/eendroroy/kotp.git"

                scm {
                    connection "scm:git:https://github.com/eendroroy/kotp.git"
                    developerConnection "scm:git:https://github.com/eendroroy/kotp.git"
                    url "https://github.com/eendroroy/kotp.git"
                }

                licenses {
                    license {
                        name "MIT"
                        url "http://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id "eendroroy"
                        name "indrajit"
                        email "eendroroy@gmail.com"
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

testlogger {
    slowThreshold 10
}

test {
    useJUnitPlatform()
}

dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/docs"

    configuration {
        includeNonPublic = false
        skipEmptyPackages = true
        skipDeprecated = false
        reportUndocumented = true
        jdkVersion = 8
    }
}